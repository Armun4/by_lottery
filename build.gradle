plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.0'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'com.diffplug.spotless' version "6.23.0"
    id "org.flywaydb.flyway" version "6.2.3"

}

group = 'com.bynder.lottery'
version = '1.0.0-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
        vendor = JvmVendorSpec.ADOPTIUM
    }
}

repositories {
    mavenCentral()
}

ext {
    set('testcontainers.version', "1.19.3")
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa:3.2.3'
    implementation 'org.springframework.boot:spring-boot-starter-web:3.2.3'
    implementation 'org.postgresql:postgresql:42.7.2'
    implementation("org.flywaydb:flyway-core:10.0.1")
    implementation("org.flywaydb:flyway-database-postgresql:10.0.1")

    runtimeOnly 'org.postgresql:postgresql'
    testImplementation 'org.springframework.boot:spring-boot-starter-test:3.2.3'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:postgresql'
    testImplementation 'io.rest-assured:rest-assured'
    testImplementation 'net.jqwik:jqwik:1.8.2'
    testImplementation 'org.assertj:assertj-core:3.25.3'
    testImplementation 'org.mockito:mockito-core:5.10.0'


    compileOnly 'org.projectlombok:lombok:1.18.20'
    annotationProcessor 'org.projectlombok:lombok:1.18.20'
}


tasks.named('test') {
    useJUnitPlatform()
    testLogging {
        events = ["PASSED", "FAILED", "SKIPPED"]
        showStandardStreams = true
        exceptionFormat = "full"
    }
}

sourceSets {
    intTest {
        java {
            compileClasspath += sourceSets.main.output + configurations.testRuntimeClasspath
            runtimeClasspath += output + compileClasspath
        }
    }
}


task intTest(type: Test) {
    group = "verification"
    testClassesDirs = sourceSets.intTest.output.classesDirs
    classpath = sourceSets.intTest.runtimeClasspath
}

tasks.named('intTest') {
    useJUnitPlatform()
    testLogging {
        events = ["PASSED", "FAILED", "SKIPPED"]
        showStandardStreams = true
        exceptionFormat = "full"
    }
}

tasks.check.dependsOn(intTest)

spotless {
    java {
        target("src/**/*.java")
        googleJavaFormat()
        removeUnusedImports()
        trimTrailingWhitespace()
        indentWithSpaces()
        endWithNewline()
    }

    format("misc") {
        target(
                ".gitignore",
                ".gitattributes",
                ".gitconfig",
                ".editorconfig",
                "*.md",
                "src/**/*.md",
                "*.sh",
                "src/**/*.properties"
        )
        indentWithSpaces()
        trimTrailingWhitespace()
        endWithNewline()
    }
}

task bootRunWithDockerCompose(dependsOn: 'dockerComposeUp') {
    doLast {
        tasks.bootRun.execute()
    }
}

task dockerComposeUp(type: Exec) {
    commandLine 'docker-compose', 'up', '-d'
}

bootRun.dependsOn dockerComposeUp

flyway {
    url = 'jdbc:postgresql://localhost:5432/postgres'
    user = 'bynder_lottery'
    password = 'bynder_lottery'
    driver = 'org.postgresql.Driver'
    locations = ['filesystem:./sql'] // Path to your migration scripts
    baselineOnMigrate = true
    baselineVersion = '1'
}
